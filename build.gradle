plugins {
	id 'java'
	id 'maven-publish'
	id 'java-gradle-plugin'
	id 'eclipse'
}

def ENV = System.getenv()

base {
	archivesName = project.name
}
version = '41.2.21'
group = project.maven_group

//if (!ENV.MAVEN_URL) {
//	version += '+local'
//}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net'
	}
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
	maven {
		name = 'Ornithe'
		url = 'https://maven.ornithemc.net/releases'
	}
	mavenCentral()
}

dependencies {
	implementation gradleApi()

	// Remember to update ProcessorSettings.PROCESSOR_VERSION when
	// one of the dependencies for jar processing is updated!

	implementation 'de.undercouch.download:de.undercouch.download.gradle.plugin:5.6.0'
	implementation 'commons-io:commons-io:2.8.0'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.google.guava:guava:33.1.0-jre'
	implementation 'com.vdurmont:semver4j:3.1.0'

	implementation 'org.ow2.asm:asm:9.8'
	implementation 'org.ow2.asm:asm-commons:9.8'
	implementation 'org.ow2.asm:asm-tree:9.8'
	implementation 'org.ow2.asm:asm-util:9.8'

	implementation 'net.ornithemc:enigma-cli:1.2.7'
	implementation 'net.ornithemc:stitch:0.24.0'
	implementation 'net.ornithemc:mapping-utils:0.19.1'
	implementation 'net.ornithemc:condor:1.4.5'
	implementation 'net.ornithemc:exceptor:1.1.0'
	implementation 'net.ornithemc:preen:1.1.0'
	implementation 'net.ornithemc:nester:1.5.0'

	implementation "net.fabricmc.unpick:unpick:2.3.0"
	implementation "net.fabricmc.unpick:unpick-format-utils:2.3.0"
	implementation "net.fabricmc.unpick:unpick-cli:2.3.0"
	implementation 'net.fabricmc:tiny-remapper:0.9.0'
	implementation 'net.fabricmc:name-proposal:0.2.0'
	implementation 'net.fabricmc:mapping-io:0.5.1'
	implementation 'net.fabricmc.filament:net.fabricmc.filament.gradle.plugin:0.10.1'

	implementation 'io.github.gaming32:signature-changer:1.0.0'
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

jar {
	manifest {
		attributes 'Implementation-Version': project.version
	}
}

gradlePlugin {
	plugins {
		keratin {
			id = 'lf-keratin'
			implementationClass = 'net.ornithemc.keratin.KeratinGradlePlugin'
		}
	}
}

import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node

publishing {
	publications {
		plugin(MavenPublication) {
			groupId group
			artifactId base.archivesName.get()
			version version

			from components.java
		}
	}
	repositories {
		if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
			maven {
				url "https://repo.legacyfabric.net/legacyfabric/"
				credentials {
					username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
					password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}
